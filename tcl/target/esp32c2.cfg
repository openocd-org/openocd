# SPDX-License-Identifier: GPL-2.0-or-later
#

# Source the ESP common configuration file.
source [find target/esp_common.cfg]

# Target specific global variables
set _CHIPNAME                   "esp32c2"
set _CHIP_HP_NAME               $_CHIPNAME
set _CPUTAPID                   0x0000cc25
set _ESP_ARCH                   "riscv"
set _ONLYCPU                    0x01
set _HP_TAPNUM                  1
set _ESP_EFUSE_MAC_ADDR_REG     0x60008840

# Set workarea address and size
# stub flasher may need a lot of memory in case of compressed writes to flash (~107KB):
# - for apptrace: 2x16KB up buffers + 32KB down buffer
# - for uncompression: 32KB for unzip buffer size + 11KB for inflator data structs
# TODO: In general when up buffers are swapped apptrace copies `host->target` data from new up buffer to down buffer to free space for `target->host` data.
# In case of flash writes we use apptrace transfers in one direction only. So we can avoid copying and re-use up buffer instead of down one.
set _WA_ADDR                0x3FCA0000
set _WA_SIZE                0x24000

# Target specific functions should be implemented for each riscv chips.
proc esp32c2_wdt_disable { } {
    # Halt event can occur during config phase (before "init" is done).
    # Ignore it since mww commands don't work at that time.
	if { [string compare [command mode] config] == 0 } {
		return
	}

	# Disable Timer Group 0 WDT
	mww 0x6001f064 0x50D83AA1
	mww 0x6001F048 0
	# Clear TG0 wdt interrupt state
	mww 0x6001F07C 0x2

	# Disable RTC WDT
	mww 0x6000809C 0x50D83AA1
	mww 0x60008084 0

	# Disable Super WDT
	mww 0x600080A4 0x8F1D312A
	mww 0x600080A0 0x84B00000

	# Clear RTC and Super wdt interrupt states
	mww 0x60008044 0x8008
}

proc esp32c2_soc_reset { } {
	global _RISCV_DMCONTROL

	# This procedure does "digital system reset", i.e. resets
	# all the peripherals except for the RTC block.
	# It is called from reset-assert-post target event callback,
	# after assert_reset procedure was called.
	# Since we need the hart to execute a write to RTC_CNTL_SW_SYS_RST,
	# temporarily take it out of reset. Save the dmcontrol state before
	# doing so.
	riscv dmi_write $_RISCV_DMCONTROL   0x80000001
	# Trigger the reset
	mww 0x60008000 0x9c00a000
	# Workaround for stuck in cpu start during calibration.
	# By writing zero to TIMG_RTCCALICFG_REG, we are disabling calibration
	mww 0x6001F068 0
	# Wait for the reset to happen
	sleep 10
	poll
	# Disable the watchdogs again
	esp32c2_wdt_disable

	# Here debugger reads allresumeack and allhalted bits as set (0x330a2)
	# We will clean allhalted state by resuming the core.
	riscv dmi_write $_RISCV_DMCONTROL   0x40000001

	# Put the hart back into reset state. Note that we need to keep haltreq set.
	riscv dmi_write $_RISCV_DMCONTROL   0x80000003
}

proc esp32c2_memprot_is_enabled { } {
	set PMP_RWX     0x07
	set PMP_RW      0x03
	set PMP_A       0x18
	set PMP_A_TOR   0x08

	set pmp1cfg [pmp_read_pmpxcfg 1]
	set pmp3cfg [pmp_read_pmpxcfg 3]

	# Check IRAM access
	if {($pmp1cfg & $PMP_RWX) != $PMP_RWX && ($pmp1cfg & $PMP_A) == $PMP_A_TOR} {
		return 1
	}

	# Check DRAM access
	if {($pmp3cfg & $PMP_RW) != $PMP_RW && ($pmp3cfg & $PMP_A) == $PMP_A_TOR} {
		return 1
	}

	return 0
}

create_esp_target $_ESP_ARCH

$_TARGETNAME_0 riscv expose_csrs 2016=mpcer
$_TARGETNAME_0 riscv expose_csrs 2017=mpcmr
$_TARGETNAME_0 riscv expose_csrs 2018=mpccr
$_TARGETNAME_0 riscv expose_csrs 2051=cpu_gpio_oen
$_TARGETNAME_0 riscv expose_csrs 2052=cpu_gpio_in
$_TARGETNAME_0 riscv expose_csrs 2053=cpu_gpio_out

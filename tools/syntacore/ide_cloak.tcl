#!/usr/bin/expect

# SPDX-License-Identifier: GPL-2.0-or-later

# This script is used to generate a list of RISC-V CSRs that are not
# "IDE friendly"
#
# Copyright (c) 2023-2025, Syntacore LLC

variable sourceDirectory [file normalize [file dirname [info script]]/../..]

proc getRISCVEncodingPath {} {
  variable sourceDirectory
  return $sourceDirectory/src/target/riscv/encoding.h
}

proc csrAddressFromName { csr_name } {
  set CSR_NAME_U [string toupper $csr_name]
  set REGISTER_ADDR_STRING [exec grep "#define CSR_$CSR_NAME_U " [getRISCVEncodingPath]]
  set ADDRESS [scan [lindex $REGISTER_ADDR_STRING 2] %x]
  return $ADDRESS
}

proc generateAllCsrAddresses {} {
  set csr_addresses {}
  for {set i 1} {$i < 4096} {incr i} {
    set hex_addr [format "0x%x" $i]
    set pattern "^#define CSR_\\S* ${hex_addr}\$"
    if {![catch {exec grep "$pattern" [getRISCVEncodingPath]}]} {
      lappend csr_addresses $i
    }
  }
  return $csr_addresses
}
proc generateListOfCloakedAddresses {VISIBLE_CSRS} {
  set CSRS_TO_HIDE [generateAllCsrAddresses]
  foreach NAME $VISIBLE_CSRS {
    set ADDRESS [csrAddressFromName $NAME]
    set IDX [lsearch $CSRS_TO_HIDE $ADDRESS]
    set CSRS_TO_HIDE [lreplace $CSRS_TO_HIDE $IDX $IDX]
  }
  return $CSRS_TO_HIDE
}

proc cloakedRecord { RANGE_START RANGE_END } {
  if { ${RANGE_START} == ${RANGE_END} } {
    return ${RANGE_START}
  }
  return "${RANGE_START}-${RANGE_END}"
}
proc calculateCloak { CSRS_TO_HIDE } {
  set CLOAK {}
  set PREV [lindex $CSRS_TO_HIDE 0]
  set RANGE_START $PREV
  foreach ITEM [lreplace $CSRS_TO_HIDE 0 0] {
    if { [expr {$ITEM - $PREV}] != 1 } {
      lappend CLOAK [cloakedRecord ${RANGE_START} ${PREV}]
      set RANGE_START $ITEM
    }
    set PREV $ITEM
  }
  lappend CLOAK [cloakedRecord ${RANGE_START} ${PREV}]
  return $CLOAK
}

proc generateIdeCloak { } {
  set IDE_CSRS [list \
    cycle \
    fcsr \
    fflags \
    frm \
    instret \
    marchid \
    mcause \
    mcounteren \
    mcycle \
    medeleg \
    mepc \
    mhartid \
    mideleg \
    mie \
    mimpid \
    minstret \
    mip \
    misa \
    mscratch \
    mstatus \
    mtval \
    mtvec \
    mvendorid \
    satp \
    scause \
    scounteren \
    sepc \
    sie \
    sip \
    sscratch \
    sstatus \
    stval \
    stvec \
    time \
    vl \
    vlenb \
    vstart \
    vtype \
    vxrm \
    vxsat \
    sedeleg \
    sideleg \
    uscratchcsw \
    uscratchcswl \
  ]

  set CSRS_TO_HIDE [generateListOfCloakedAddresses $IDE_CSRS]
  set CLOAK [calculateCloak $CSRS_TO_HIDE]

  return [join [list \
    "# SPDX-License-Identifier: GPL-2.0-or-later\n\n" \
    "# Copyright (c) 2023-2025, Syntacore LLC\n\n" \
    "# NOTE: this file was auto-generated by `[file tail [info script]]` script\n" \
    "proc sc_target_ide_csr_cloak {} {\n\treturn \"[join $CLOAK ","]\"\n}" \
  ] ""]
}

set cloakFunction [generateIdeCloak]

if { $argc != 1 } {
  puts $cloakFunction
} else {
  set FP [open [lindex $argv 0] w+]
  puts $FP "$cloakFunction"
  close $FP
}
